/*
 * Copyright (c) 2017. HÃ©ctor de Isidro - hrules6872
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'

android {
  compileSdkVersion androidBuildCompileSDKVersion
  buildToolsVersion androidBuildToolsVersion

  defaultConfig {
    applicationId "com.hrules.cryptoprofit"
    minSdkVersion androidBuildMinSDKVersion
    targetSdkVersion androidBuildTargetSDKVersion
    versionCode appVersionCode
    versionName appVersionName

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    release {
      Properties appSecretProps = new Properties()
      appSecretProps.load(new FileInputStream(new File('app.secrets.properties')))

      keyAlias appSecretProps['APP_RELEASE_KEYALIAS']
      keyPassword appSecretProps['APP_RELEASE_KEYPASSWORD']
      storeFile file(appSecretProps['APP_RELEASE_STOREFILE'])
      storePassword appSecretProps['APP_RELEASE_STOREPASSWORD']
    }
  }

  buildTypes {
    debug {
      resValue 'string', 'app_name', '"CryptoProfit-debug"'
      debuggable true
      minifyEnabled false
      zipAlignEnabled false
      applicationIdSuffix ".debug"
    }

    release {
      resValue 'string', 'app_name', '"CryptoProfit"'
      minifyEnabled false
      signingConfig signingConfigs.release
    }
  }

  productFlavors {}

  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }
}

kotlin {
  experimental {
    coroutines "enable"
  }
}

dependencies {
  testDependencies.each {
    add("testImplementation", it.value)
  }

  androidTestDependencies.each {
    add("androidTestImplementation", it.value)
  }

  supportDependencies.each {
    add("implementation", it.value)
  }

  projectDependencies.each {
    add("implementation", it.value)
  }
}
